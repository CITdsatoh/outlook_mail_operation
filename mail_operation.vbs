Option Explicit

Class AddrNumSet

  Public DstAddress
  Public DstName
  Public FirstDate
  Public mailNum
  Public State
  Public IsFirst
  Public DataPool ()
  
  Public Sub Class_Initialize()
    DstAddress=""
    DstName=""
    mailNum=0
    State="削除済みへ移動"
    ReDim DataPool(0)
    IsFirst=True
  End Sub
  
  Public Sub Class_Terminate()
  End Sub
  
  Public Function SetValue(Addr,Name,NumStr,MailState)
  
   DstAddress=Addr
   DstName=Name
   mailNum=CLng(NumStr)
   State=MailState
   
  End Function
  
  Public Function SetFirstDate(FDate)
    FirstDate=CDate(FDate)
  End Function
  
  Public Function NumIncrement()
   mailNum=mailNum+1
  End Function
  
  Public Function GetAddress()
   GetAddress=DstAddress
  End Function
  
  Public Function GetNum()
   GetNum=mailNum
  End Function
  
  Public Function GetName()
   GetName=DstName
  End Function
  
  Public Function GetMailState()
   GetMailState=State
  End Function
  
  Public Function GetFirstDate()
   If Not IsEmpty(FirstDate) Then
    GetFirstDate=CDate(FirstDate)
    Exit Function
   End If
   
   GetFirstDate=DataPool(1)
   FirstDate=DataPool(1)
   Dim i
   For i=2 To UBound(DataPool)
     If DataPool(i) < GetFirstDate Then
       GetFirstDate=DataPool(i)
       FirstDate=DataPool(i)
     End If
   Next
   
  End Function
  
  Public Function AddDataPool(NewDate)
    If IsEmpty(FirstDate) Then
     ReDim Preserve DataPool(UBound(DataPool)+1)
     DataPool(UBound(DataPool))=NewDate
    End If
  End Function
  
  Public Function ToStr()
   ToStr=DstAddress&","&DstName&","&FirstDate&","&mailNum&","&State
  End Function

End Class

  
Class Manager

  Public AddrNumLists ()
  Public FileName
  Public BackupFileName
  Public LogFile
  Public WshObj
  Public FDate
  
  Public Sub Class_Initialize()
   ReDim AddrNumLists(0)
   FileName="outlook_mail_dest_list.csv"
   Set WshObj=Wscript.CreateObject("Wscript.Shell")
   Dim BackupFolder
   BackupFolder=WshObj.SpecialFolders(5)
   BackupFileName=BackupFolder&"\"&FileName
   LogFile=BackupFolder&"\datelog.log"
   SetFileDate
  End Sub
  
  Public Sub Class_Terminate()
  End Sub
  
  'メールをカウントする(カウントが0、つまりまだその宛先からのメールが存在しない場合は,新しくオブジェクトを作る)
  Public Function Count(addr,name,date)
   Dim i
   For i=1 To UBound(AddrNumLists)
     If addr = AddrNumLists(i).GetAddress And name = AddrNumLists(i).GetName Then
       AddrNumLists(i).NumIncrement()
       AddrNumLists(i).AddDataPool date
       Exit Function
     End If
   Next
   
   ReDim Preserve AddrNumLists (UBound(AddrNumLists)+1)
   Dim NumObj
   Set NumObj=new AddrNumSet
   NumObj.SetValue addr,name,"1","削除済みへ移動"
   NumObj.AddDataPool date
   Set AddrNumLists(UBound(AddrNumLists))=NumObj
   
  End Function
  
  'その宛先からのメールの数を返す
  Public Function getNum(addr,name)
   For i=LBound(AddrNumLists) To UBound(AddrNumLists)
     If addr = AddrNumLists(i).GetAddress And AddrNumLists(i).getName = name Then
       getNum=AddrNumLists(i).getNum()
       Exit Function
     End If
   Next
   getNum=0
  End Function
  
  'その宛先のメールをどのように扱うか(削除済みへ移動（デフォルト)か、保存か,完全削除か)
  Public Function GetState(addr,name)
    Dim i
    For i=1 To UBound(AddrNumLists)
     If addr = AddrNumLists(i).GetAddress And name = AddrNumLists(i).GetName Then
       GetState=AddrNumLists(i).GetMailState()
       Exit Function
     End If
   Next
   GetState="削除済みへ移動"
  End Function
  
  'メールを調べるにあたり,保存メールの重複カウントを避けるため,前回、メールの数をカウントしたのはいつなのかを得る
  '保存メールに入っている,この日付より前のメールに関してはすでにカウントしているのでカウントしない
  Public Function GetModifiedFileDate()
    GetModifiedFileDate=FDate
  End Function
 
  Public Function SetFileDate()
   Dim FObj
   Dim FSObj
   Set FSObj=CreateObject("Scripting.FileSystemObject")
   'ログファイルから最後にメールをチェックした日付情報を得る
   If FSObj.FileExists(LogFile) Then
     FSObj.GetFile(LogFile).attributes=0
     FDate=GetRealModifiedDate
   ElseIf FSObj.FileExists(FileName) Then
     'Logファイルがなかった場合この日付で代用する
     FDate=FSObj.GetFile(FileName).DateLastModified
   ElseIf FSObj.FileExists(BackupFileName) Then
     FDate=FSObj.GetFile(BackupFileName).DateLastModified
   Else
     FDate=CDate("1970/1/1")
   End If
  
  End Function
     
   
  Public Function FRead()
    Dim FObj
    Dim FSObj
    Set FSObj=CreateObject("Scripting.FileSystemObject")
    If FSObj.FileExists(FileName) Then
      Set FObj=FSObj.GetFile(FileName)
      FReading FileName
    ElseIf FSObj.FileExists(BackupFileName) Then
      Set FObj=FSObj.GetFile(BackupFileName)
      FSObj.attributes=0
      FReading BackupFileName
    End If
    
   
  End Function
  
  Public Function  FReading(FName)
   Dim FReader
    
   Set FReader=Wscript.CreateObject("ADODB.Stream")
   FReader.Type=2
   FReader.Charset="UTF-8"
   FReader.LineSeparator=10
   FReader.Open
   FReader.LoadFromFile FName
     
   '1行目はヘッダなので情報として必要ない。
   'とりあえず,1回全部の行についてデータを取得する
   Dim AllData ()
   Dim LineNum
   Dim OneLine
   LineNum=0
   Do While FReader.EOS = False
     ReDim Preserve AllData(LineNum)
     OneLine=FReader.ReadText(-2)
     AllData(LineNum)=OneLine
     LineNum=LineNum+1
   Loop
     
   FReader.Close
   Set FReader=Nothing
   Dim OneData
   Dim NumObj
   Dim i
   Dim AllDataWithoutBr
     
   'ヘッダはいらないので1番目から取得する。そして最後の行は合計なのでそれもいらない
   For i=1 To UBound(AllData)-1
     AllDataWithoutBr=Replace(AllData(i),VbCr,"")
     OneData=Split(AllDataWithoutBr,",")
     ReDim Preserve AddrNumLists(UBound(AddrNumLists)+1)
     Set NumObj=new AddrNumSet
     If UBound(OneData) = 4 Then
       NumObj.SetValue OneData(0),oneData(1),OneData(3),OneData(4)
     ElseIf UBound(OneData) >= 3 Then
       NumObj.SetValue OneData(0),oneData(1),OneData(3),"削除済みへ移動"
     End If
     NumObj.SetFirstDate OneData(2)
     Set AddrNumLists(UBound(AddrNumLists))=NumObj
   Next
      
  End Function
  
  Public Function GetRealModifiedDate()
    Dim DateDataStr
    Dim FReader
    Set FReader=Wscript.CreateObject("ADODB.Stream")
    FReader.Type=2
    FReader.Charset="UTF-8"
    FReader.LineSeparator=10
    FReader.Open
    FReader.LoadFromFile LogFile
    Do While FReader.EOS = False
      DateDataStr=FReader.ReadText(-2)
      Exit Do
    Loop
    
    FReader.Close
    Set FReader=Nothing
    getRealModifiedDate=CDate(Replace(DateDataStr,VbCr,""))
    
  End Function
  
  '初めてファイルに書き込む際,表示順を時間順にする必要がある
  Public Function DateSort()  
    Dim i
    Dim j
    Dim tmp
    Dim head
    head=1
    
    
   For i=2 To UBound(AddrNumLists)
      
      j=i
      'VBSでは短絡評価してくれないので,一番最初のインデックスより前のインデックスにアクセスしてしまいエラーが出るので
      'ここでは,一番最初のインデックスの1つ前までのインデックスについてソートし
      Do While (j > head+1) And(AddrNumLists(j-1).GetFirstDate() > AddrNumLists(j).GetFirstDate())
        Set tmp=AddrNumLists(j)
        Set AddrNumLists(j)=AddrNumLists(j-1)
        Set AddrNumLists(j-1)=tmp
        j=j-1
      Loop
      
      '最後のインデックスだけここで別途行う
      If (AddrNumLists(head).GetFirstDate() > AddrNumLists(head+1).GetFirstDate()) Then
        Set tmp=AddrNumLists(head)
        Set AddrNumLists(head)=AddrNumLists(head+1)
        Set AddrNumLists(head+1)=tmp
      End If
      
  Next
        
  End Function    
  
  Public Function FWrite()
  
    DateSort()

    
    Dim Header
    Dim Today
    Dim FWriter
    Today=Date()
    Dim FirstDate
    FirstDate=AddrNumLists(1).GetFirstDate
    
    Header="メールアドレス,宛名,"&FirstDate&"以降で最も早くその宛先からメールが届いた日付,"&FirstDate&"から"&Today&"までに届いたメールの数,メールの取り扱い"
    Set FWriter=Wscript.CreateObject("ADODB.Stream")
    FWriter.Type=2
    FWriter.Charset="UTF-8"
    FWriter.Open
    FWriter.WriteText Header,1
    
    Dim i
    
    '合計メール数(1番最後の行に書いておく)
    Dim MailSum
    MailSum=0
    For i= 1 To UBound(AddrNumLists)
      MailSum=MailSum+AddrNumLists(i).GetNum()
      FWriter.WriteText AddrNumLists(i).ToStr(),1
    Next
    
    FWriter.WriteText "合計,,,"&MailSum&",",1
    
    FWriter.SaveToFile FileName,2
    FWriter.Close
    
    Set FWriter=Nothing
     
    Dim CpyFso
    Set CpyFso=Wscript.CreateObject("Scripting.FileSystemObject")
    'バックアップファイルがない場合エラーが出る
    On Error Resume Next
      CpyFso.GetFile(BackupFileName).attributes=0
    On Error Goto 0
    
    CpyFso.CopyFile FileName,BackupFileName,True
    CpyFso.GetFile(BackupFileName).attributes=1
    Set CpyFso=Nothing
   
    For i=LBound(AddrNumLists) To UBound(AddrNumLists)
      Set AddrNumLists(i)=Nothing
    Next
    
  End Function
  
  'チェックした日付をログに記録
  Public Function LogWrite(LastMailDate)
    Dim FWriter
    Set FWriter=Wscript.CreateObject("ADODB.Stream")
    FWriter.Type=2
    FWriter.Charset="UTF-8"
    FWriter.Open
    FWriter.WriteText ""&LastMailDate,1
    FWriter.SaveToFile LogFile,2
    FWriter.Close
    Set FWriter=Nothing
    Wscript.CreateObject("Scripting.FileSystemObject").GetFile(LogFile).attributes=1
  End Function
  
  
      

End Class

Function MailEquals(One,Other)
 
 MailEquals=True
 If One.SenderEMailAddress <> Other.SenderEMailAddress Then
   MailEquals=False
   Exit Function
 End If
 
 If One.SenderName <> Other.SenderName Then
   MailEquals=False
   Exit Function
 End If
 
 If One.ReceivedTime <> Other.ReceivedTime Then
   MailEquals=False
   Exit Function
 End If
 
 If One.Subject <> Other.Subject Then
   MailEquals=False
   Exit Function
 End If
 
End Function

Function HasMailInFolder(MailItem,Folder)
  Dim i
  For i=1 To Folder.Items.Count
   If MailEquals(MailItem,Folder.Items(i)) Then
     HasMailInFolder=True
     Exit Function
   End If
  Next
  HasMailInFolder=False

End Function


Function Main()

  Dim outlook
  Dim namespace
  Dim receiveFolder
  Dim mailItems
  Dim Wsh

  Dim addressInfo()

  'outlookにアクセス
  Set outlook=Wscript.CreateObject("Outlook.Application")

  'outlookのメールにアクセスするためのインターフェース
  Set namespace=outlook.GetNameSpace("MAPI")
  

  'outlookの受信フォルダ
  Set receiveFolder=namespace.GetDefaultFolder(6)
  
  '削除済みフォルダ
  Dim deletedFolder
  Set deletedFolder=namespace.GetDefaultFolder(3)
  
  
  'outlookを最小化して起動(相手にoutlookが起動していることがわからないようにする)
  Set Wsh=Wscript.CreateObject("Wscript.shell")
  Wsh.Run "outlook.exe",7,False


  'メールのカウントなどを行う管理クラス
  Dim CountManager

  Set CountManager=new Manager
  
  
  'ファイル読込
  CountManager.FRead()
  
 
  
  'メールの扱い方(メールの宛先によってメールの処理の仕方を変える)
  '削除済みへ移動（デフォルト)か,完全削除か,保存か
  Dim MailOperation
 
  'メール
  Dim OneMailItem
   
  'もしもとっておきたいメールが出たらこの変数を用いてとっておきたいメールの数を保存しておく
  'つまりメールを操作する際のインデックス
  Dim SaveMailNum
  SaveMailNum=0
  
  Dim CurrentMailNum
 
 
   
  'まずは削除済みアイテムについてみてゆく
  Do While SaveMailNum < deletedFolder.Items.Count
     CurrentMailNum=deletedFolder.Items.Count
     Set OneMailItem=deletedFolder.Items.Item(SaveMailNum+1)
     MailOperation=CountManager.GetState(OneMailItem.SenderEMailAddress,OneMailItem.SenderName)
     If MailOperation = "保存" Then
       OneMailItem.Move receiveFolder
       Do While  CurrentMailNum = deletedFolder.Items.Count 
           Wscript.Sleep 1000
       Loop
     ElseIf MailOperation = "完全削除" Then
       OneMailItem.Delete
       Do While  CurrentMailNum = deletedFolder.Items.Count 
           Wscript.Sleep 1000
       Loop
     Else
       SaveMailNum=SaveMailNum+1
     End If
  Loop
   
  
  '削除済みアイテムの方を見たので次は受信済みの方を見る
  Set OneMailItem=Nothing
  '保存（フォルダキープ）のメール数を0に戻す
  SaveMailNum=0
  MailOperation=""
 
  '次は受信アイテムの中を見てゆくが,受信アイテムの読み込みとこの処理は非同期であることから少しタイムラグを設ける必要がある
  'タイムラグ用の変数
  Dim NormSec
  NormSec=30
 
 
  Dim CountTimes
  CountTimes=0
 
  Dim CountWaitSec
  CountWaitSec=0
 
  Dim EnterFlag
  EnterFlag=False
 
  '最後にいつメールのカウントを行ったのかを得る(保存フォルダから重複カウントをしないように)
  Dim LastCountMailDate
  LastCountMailDate=CountManager.GetModifiedFileDate()
  
  Dim TestFolder
  Set TestFolder=deletedFolder.Folders("test")
  
  
  'カウンタ変数
  Dim i
  
  'メールの情報
  Dim Addr
  Dim Name
  Dim Time
  CurrentMailNum=receiveFolder.Items.Count
  
  Dim TestFolderMailNum
  
  
  Dim LastMailTime
  LastMailTime=LastCountMailDate
 
  Do While CountWaitSec < NormSec 
    Do While SaveMailNum < receiveFolder.Items.Count
      'メールを消去(削除済みフォルダ）に移動させると,受信トレーのメールが減るので,ずっと,同じインデックスをアクセスする
      EnterFlag=True
      CurrentMailNum=receiveFolder.Items.Count
      Set OneMailItem=receiveFolder.Items.Item(SaveMailNum+1)
      Name=OneMailItem.SenderName
      Addr=OneMailItem.SenderEmailAddress
      Time=OneMailItem.ReceivedTime
      
      If HasMailInFolder(OneMailItem,deletedFolder) Then
         TestFolderMailNum=TestFolder.Items.Count
         OneMailItem.Move TestFolder
         Do While  TestFolderMailNum = TestFolder.Items.Count
           Wscript.Sleep 1000
           Wscript.Echo "待機中"
         Loop
      Else
        If LastCountMailDate < Time Then
          CountManager.Count Addr,Name,Time
        End If
        
        If LastMailTime < Time Then
          LastMailTime=Time
        End If
        
       
        MailOperation=CountManager.GetState(Addr,Name)
          
        'メールの扱い方（宛先によってメールをどう扱うか)
        '保存する場合
        If MailOperation = "保存" Then
          'このメールは消さずに参照するメールのインデックスを1進める
          SaveMailNum=SaveMailNum+1
        ElseIf MailOperation = "完全削除" Then
          OneMailItem.Delete
          '削除が完了するまで待つ
          Do While  CurrentMailNum = receiveFolder.Items.Count 
            Wscript.Sleep 1000
          Loop
        Else
          OneMailItem.Move deletedFolder
          '移動が完了するまで待つ
          Do While  CurrentMailNum = receiveFolder.Items.Count
            Wscript.Sleep 1000
          Loop
        End If
      End If 
       
        
    Loop
      
    If EnterFlag Then
      CountWaitSec=0
      If CountTimes <= 100 Then
        CountTimes=CountTimes+1
      End If
      Dim Bias
      Bias=(CountTimes\5)+1
      NormSec=30\Bias
      EnterFlag=False
    End If
      
    Wscript.Sleep 1000
      
    CountWaitSec=CountWaitSec+1
    
  Loop
  
  '結果をファイルに記入
  CountManager.FWrite()
  CountManager.LogWrite(LastMailTime)
  
  Set OneMailItem=Nothing
  
  Set CountManager=Nothing
  
  Dim Fold
  Set Fold=namespace.GetDefaultFolder(20)
  
  Do While Fold.Items.Count <> 0
    Fold.Items(1).Delete
    Wscript.Sleep 1000
  Loop
  
  Dim Explorer
  Set Explorer=outlook.ActiveExplorer
  
  'Explorerが取得できなかったら,強制終了
  If Explorer Is Nothing Then
    'Outlookの終了
    Dim Locator
    Dim Service
    Dim oProc
    Dim oProcs
    
    Set Locator = WScript.CreateObject("WbemScripting.SWbemLocator")
    Set Service = Locator.ConnectServer
    Set oProcs = Service.ExecQuery("Select * From Win32_Process Where Description=""OUTLOOK.EXE""")
    
    For Each oProc In oProcs
       oProc.Terminate
    Next
    
  Else
    Explorer.Close
  End If
  
End Function

Main